# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=build
pkgname="python-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="A simple, correct Python build frontend"
arch=('any')
url='https://github.com/pypa/build'
msys2_references=(
  "purl: pkg:pypi/build"
)
license=('spdx:MIT')
depends=("python-packaging"
         "python-pyproject-hooks")
makedepends=(
  "python-flit-core"
  "python-installer"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('698edd0ea270bde950f53aed21f3a0135672206f3911e0176261a31e0e07b397')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}
