# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=python-fastbencode
pkgver=0.3.2
pkgrel=1
pkgdesc='Implementation of bencode with optional fast C extensions'
arch=('i686' 'x86_64')
url='https://github.com/breezy-team/fastbencode'
msys2_references=(
  "purl: pkg:pypi/fastbencode"
)
license=(spdx:GPL-2.0-or-later)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-devel
  cython
  gcc
)
checkdepends=(python-pytest)
source=(https://pypi.io/packages/source/f/fastbencode/fastbencode-$pkgver.tar.gz)
sha256sums=('a34c32c504b0ec9de1a499346ed24932359eb46234b28b70975a50fdfaa14ab5')

build() {
  cd fastbencode-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd fastbencode-$pkgver
  python -m pytest
}

package() {
  cd fastbencode-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
